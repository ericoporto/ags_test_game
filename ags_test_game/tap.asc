// new module script
String _test_filename;
int _test_count;
int _test_max;
int _fail_count;
int _pass_count;

void _check_test_filename(){
  if(_test_filename == null) {
    _test_filename = "$SAVEGAMEDIR$/";
    _test_filename = _test_filename.Append(DEFAULT_TEST_FILENAME);
  }  
}

void _test_printer(String str){
  _check_test_filename();
  
  File* file = null;
  if(!File.Exists(_test_filename)){
    file = File.Open(_test_filename, eFileWrite);
  } else {
    file = File.Open(_test_filename, eFileAppend);
  }
  
  file.WriteRawLine(str);
  file.Close();
}

void clean_test(){
  _check_test_filename();
  
  File* file = null;
  if(File.Exists(_test_filename)){
    File.Delete(_test_filename);
  }  
}

void plan(int n,  String skip_reason){
  _test_max = n;
  _test_count = 1;
  _fail_count = 0;
  _pass_count = 0;
  if(n == NO_PLAN) return;
  if(n == SKIP_ALL) {
    // need to ignore everything until next done_testing  
    if(skip_reason != null && skip_reason.Length > 0){
      _test_printer(String.Format("SKIP %s\n", skip_reason));
    }
    return;
  } 
  _test_printer(String.Format("i..%d", n));
}

void ok(bool condition, String test_name){
  if(condition) {
    if(test_name!=null && test_name.Length > 0) {
      _test_printer(String.Format("ok %d - %s", _test_count, test_name));
    } else {
      _test_printer(String.Format("ok %d", _test_count));      
    }
    _pass_count++;
  } else {
    if(test_name!=null && test_name.Length > 0) {
      _test_printer(String.Format("not ok %d - %s", _test_count, test_name));
      _test_printer(String.Format("# Failed test '%s'", test_name));
    } else {
      _test_printer(String.Format("not ok %d", _test_count));
      _test_printer(String.Format("# Failed test", test_name));      
    }
    _fail_count++;
  }
  _test_count++;
}

void nok(bool condition, String test_name){
  ok(!condition, test_name);
}

void is(String got,  String expected, String test_name){
  bool test = got == expected;
  
  ok(test, test_name);
  if(!test){
    _test_printer(String.Format("# got: %s", got));
    _test_printer(String.Format("# expected: %s", expected));
  }
}

void isnt(String got,  String expected, String test_name){
  bool test = got != expected;
  
  ok(test, test_name);
  if(!test){
    _test_printer(String.Format("# got: %s", got));
    _test_printer(String.Format("# expected different than: %s", expected));
  }
}

void is_int(int got,  int expected, String test_name){
  bool test = got == expected;
  
  ok(test, test_name);
  if(!test){
    _test_printer(String.Format("# got: %d", got));
    _test_printer(String.Format("# expected: %d", expected));
  }
}

void isnt_int(int got,  int expected, String test_name){
  bool test = got != expected;
  
  ok(test, test_name);
  if(!test){
    _test_printer(String.Format("# got: %d", got));
    _test_printer(String.Format("# expected different than: %d", expected));
  }
}

void is_float(float got,  float expected, float epsilon, String test_name){
  bool test = (got + epsilon >= expected) && (got - epsilon <= expected);
  
  ok(test, test_name);
  if(!test){
    _test_printer(String.Format("# got: %f", got));
    _test_printer(String.Format("# expected: %f", expected));
    _test_printer(String.Format("# epsilon: %f", epsilon));
  }
}

void isnt_float(float got,  float expected, float epsilon, String test_name){
  bool test = (got + epsilon < expected) && (got - epsilon > expected);
  
  ok(test, test_name);
  if(!test){
    _test_printer(String.Format("# got: %f", got));
    _test_printer(String.Format("# expected different than: %f", expected));
    _test_printer(String.Format("# epsilon: %f", epsilon));
  }
}

void done_testing(){
  _test_printer(String.Format("# failed tests: %d", _fail_count));
  _test_printer(String.Format("# passed tests: %d", _pass_count));
  _test_printer(String.Format("# total tests: %d", _test_count-1));
  _test_printer(String.Format("# planned tests: %d", _test_max));  
}